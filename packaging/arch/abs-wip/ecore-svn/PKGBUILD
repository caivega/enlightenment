# $Id: $
# Maintainer: Gustavo Sverzut Barbieri <barbieri@gmail.com>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=ecore-svn
pkgver=78735
pkgrel=1
pkgdesc="OS abstraction layer for e17"
arch=('i686' 'x86_64')
groups=('e17-libs-svn' 'e17-svn')
url="http://www.enlightenment.org"
license=('BSD')
depends=('efl-svn' 'evas-svn' 'glib2' 'libxp' 'curl' 'libxss' 'libxtst'
  'libxcomposite' 'libxrandr' 'libxinerama' 'libxcursor')
makedepends=('subversion')
conflicts=('ecore')
provides=('ecore')
options=('!libtool' '!emptydirs')
source=()
md5sums=()

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/ecore"
_svnmod="ecore"

build() {
  cd $srcdir

  if [ $NOEXTRACT -eq 0 ]; then
    msg "Connecting to $_svntrunk SVN server...."
    if [ -d $_svnmod/.svn ]; then
      (cd $_svnmod && svn up -r $pkgver)
    else
      svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi

    msg "SVN checkout done or server timeout"
    msg "Starting make..."

  fi

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ecore \
    --with-internal-maximum-log-level=3 \
    --disable-static \
    --disable-ecore-timer-dump \
    --enable-glib \
    --enable-ecore-x-composite \
    --enable-ecore-x-damage \
    --enable-ecore-x-dpms \
    --enable-ecore-x-randr \
    --enable-ecore-x-render \
    --enable-ecore-x-screensaver \
    --enable-ecore-x-shape \
    --enable-ecore-x-gesture \
    --enable-ecore-x-sync \
    --enable-ecore-x-xfixes \
    --enable-ecore-x-xinerama \
    --enable-ecore-x-xprint \
    --enable-ecore-x-xtest \
    --enable-ecore-x-cursor \
    --enable-ecore-x-input \
    --enable-ecore-x-dri \
    --enable-epoll \
    --enable-posix-threads \
    --disable-debug-threads \
    --enable-thread-safety \
    --enable-atfile-source \
    --enable-ecore-con \
    --enable-curl \
    --disable-gnutls \
    --enable-openssl \
    --enable-ecore-ipc \
    --enable-ecore-file \
    --enable-poll \
    --enable-inotify \
    --enable-ecore-imf \
    --enable-ecore-imf-evas \
    --enable-ecore-input \
    --enable-ecore-input-evas \
    --enable-ecore-imf-xim \
    --disable-ecore-imf-scim \
    --disable-ecore-imf-ibus \
    --enable-ecore-x \
    --disable-ecore-sdl \
    --enable-ecore-fb \
    --disable-ecore-directfb \
    --enable-ecore-evas \
    --enable-ecore-evas-software-buffer \
    --enable-ecore-evas-software-x11 \
    --enable-ecore-evas-opengl-x11 \
    --enable-ecore-evas-fb \
    --disable-ecore-evas-ews \
    --disable-ecore-wayland \
    --disable-tests \
    --disable-coverage \
    --disable-install-examples \
    --disable-doc
  make
}

package(){
  cd $srcdir/$_svnmod-build
  make DESTDIR=$pkgdir install

# install license files
  install -Dm644 $srcdir/$_svnmod-build/COPYING \
        $pkgdir/usr/share/licenses/$pkgname/COPYING

  rm -r $srcdir/$_svnmod-build

}
