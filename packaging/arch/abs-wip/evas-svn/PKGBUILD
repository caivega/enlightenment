# $Id: $
# Maintainer: Gustavo Sverzut Barbieri <barbieri@gmail.com>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=evas-svn
pkgver=78859
pkgrel=1
pkgdesc="A hardware-accelerated canvas API for X-Windows"
arch=('i686' 'x86_64')
groups=('e17-libs-svn' 'e17-svn')
url="http://www.enlightenment.org"
license=('BSD')
depends=('efl-svn' 'giflib' 'libpng' 'libjpeg>=7' 'libxrender' 'mesa'
  'fribidi' 'fontconfig' 'freetype2')
makedepends=('subversion')
optdepends=("evas_generic_loaders-svn")
conflicts=('evas')
provides=('evas')
options=('!libtool' '!emptydirs')
md5sums=()

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/evas"
_svnmod="evas"

build() {
  cd "$srcdir"

  if [ $NOEXTRACT -eq 0 ]; then
    msg "Connecting to $_svntrunk SVN server...."
    if [ -d $_svnmod/.svn ]; then
      (cd $_svnmod && svn up -r $pkgver)
    else
      svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi

    msg "SVN checkout done or server timeout"
    msg "Starting make..."

  fi

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/evas \
    --enable-strict \
    --disable-static \
    --enable-fontconfig \
    --enable-fribidi \
    --disable-harfbuzz \
    --disable-pixman \
    --enable-buffer=static \
    --disable-gl-sdl \
    --enable-fb=static \
    --disable-directfb \
    --disable-wayland-shm \
    --disable-wayland-egl \
    --disable-sdl-primitive \
    --enable-static-software-generic \
    --disable-software-xcb \
    --enable-software-xlib=static \
    --disable-gl-xcb \
    --enable-gl-xlib=static \
    --disable-image-loader-webp \
    --enable-image-loader-gif \
    --disable-image-loader-tiff \
    --disable-image-loader-svg \
    --disable-install-examples \
    --disable-tests \
    --disable-coverage \
    --disable-doc
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install

# install license files
  install -Dm644 "$srcdir/$_svnmod-build/COPYING" \
        "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  rm -r "$srcdir/$_svnmod-build"

}
