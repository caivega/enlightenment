# $Id: $
# Maintainer: Gustavo Sverzut Barbieri <barbieri@gmail.com>
# Contributor: Gustavo Sverzut Barbieri <barbieri@gmail.com>

pkgname=terminology-svn
pkgver=78638
pkgrel=1
pkgdesc="Terminal Emulator in EFL"
arch=('i686' 'x86_64')
groups=('e17-libs-svn' 'e17-svn')
url="http://www.enlightenment.org"
license=('LGPL')
depends=('elementary-svn' 'desktop-file-utils')
makedepends=('subversion')
conflicts=('terminology')
provides=('terminology')
options=('!libtool' '!emptydirs')
install=terminology.install
source=()
md5sums=()

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/terminology"
_svnmod="terminology"

build() {
  cd $srcdir

  if [ $NOEXTRACT -eq 0 ]; then
    msg "Connecting to $_svntrunk SVN server...."
    if [ -d $_svnmod/.svn ]; then
      (cd $_svnmod && svn up -r $pkgver)
    else
      svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
    fi

    msg "SVN checkout done or server timeout"
    msg "Starting make..."

  fi

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  export CFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CFLAGS}"
  export CXXFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections ${CXXFLAGS}"
  export LDFLAGS="-fvisibility=hidden -ffunction-sections -fdata-sections -Wl,--gc-sections -Wl,--as-needed  ${LDFLAGS}"

  ./autogen.sh --prefix=/usr
  make
}

package(){
  cd $_svnmod-build
  make DESTDIR=$pkgdir install

  rm -r $srcdir/$_svnmod-build

}
